/*
 * Copyright (c) 2015. Gianluca Vegetti
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.iukonline.amule.android.amuleremote"
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 20
        versionName "0.6"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true

        }
    }

    productFlavors {

        dev {
            applicationId "com.iukonline.amule.android.amuleremote.dev"
            versionName "0.6-DEV-" + getDate()
        }

        snapshot {
            applicationId "com.iukonline.amule.android.amuleremote.snapshot"
            versionName "0.6-SNAPSHOT-" + getDate()
        }

        target {
            applicationId "com.iukonline.amule.android.amuleremote"
        }
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

repositories {
    mavenCentral()
    maven {
        url "https://dl.dropboxusercontent.com/u/15068406/iukonline-mvn-repository/"
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, "seconds"
    }

}

dependencies {
    compile ('com.iukonline.amule:JavaAmuleEC:0.5.1-SNAPSHOT') { changing = true ; }
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'ch.acra:acra:4.5.0'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.5@aar'
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'joda-time:joda-time:2.8.2'
}
